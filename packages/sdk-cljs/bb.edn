;; packages/sdk-cljs/bb.edn
{:paths []
 :deps {uberdeps/uberdeps {:mvn/version "1.1.2"}}
 :min-bb "0.7.0"
 :tasks
 {:requires
  [[babashka.classpath :as cp]
   [babashka.fs :as fs]
   [clojure.edn :as edn]
   [clojure.java.io :as io]
   [clojure.set :as cs]
   [clojure.string :as str]]
  :init
  (do
    ;; Get the path to the SDK root.
    (def sdk-root (str (fs/canonicalize (fs/path ".." ".."))))
    ;; Add shared build code to classpath.
    (cp/add-classpath (str (fs/path sdk-root "bb")))
    (require '[com.kubelt.cli :as cli])
    (require '[com.kubelt.environ :as environ])
    (require '[com.kubelt.package :as package])
    (require '[com.kubelt.semver :as semver])
    (require '[com.kubelt.shadow :as shadow])
    (require '[com.kubelt.time :as time])
    ;; Extract version from package.json.
    (def version (cli/release-version "package.json" *command-line-args*)))

  ;; Tasks
  ;; ---------------------------------------------------------------------------

  ;; Compare the dependencies in parent package.json and local
  ;; package.json For any deps that intersect, make sure they have the
  ;; same version. Note that this only detects the error when the
  ;; necessary dependencies are present in both files; if a dependency
  ;; is missing we can't currently detect that.
  package:check
  {:doc "Check for package.json version discrepancies"
   :task (let [pkg-parent (str (fs/file sdk-root "package.json"))
               pkg-local (str (fs/file "." "package.json"))
               mismatches (package/mismatches pkg-parent pkg-local)]
           (when (not (empty? mismatches))
             (throw (ex-info "mismatched versions" mismatches))))}
  
  build:develop
  {:doc "Build a development JavaScript version of the JS CLI"
   :depends [package:check]
   :task (println "Not implemented")}

  build:release
  {:doc "Build a release JavaScript version of the JS CLI"
   :depends [package:check]
   :task (shell "echo 'Not implemented'")}

  test:develop
  {:doc "Run ClojureScript tests against development build of SDK"
   :task (shell {:dir sdk-root} "npx shadow-cljs compile sdk-test")}

  test:release
  {:doc "Run ClojureScript tests against release build of SDK"
   :task (shell {:dir sdk-root} "npx shadow-cljs release sdk-test")}

  ;; Using uberdeps to generate the JAR from a generated deps.edn
  ;; includes Clojure class files, which aren't needed to distribute a
  ;; CLJS library. Try another jar packing tool; in the interim we
  ;; generate a project.clj and rely on lein.
  generate:cljs:deps
  {:doc "Generate a deps.edn from shadow-cljs.edn"
   :task
   (letfn [(to-deps-edn [[k v]]
             [k {:mvn/version v}])]
     (let [shadow-path (str (fs/path sdk-root "shadow-cljs.edn"))
           output-path (str (fs/path sdk-root "deps.edn"))
           ;; Extract dependencies from shadow-cljs.edn
           shadow-reader (java.io.PushbackReader. (io/reader shadow-path))
           shadow-map (edn/read shadow-reader)
           deps-vec (get shadow-map :dependencies [])
           deps-map (into {} (map to-deps-edn deps-vec))
           ;; Build a deps.edn file
           deps-edn {:paths ["src/main"]
                     :dependencies deps-map}]
       (spit output-path (pr-str deps-edn))))}

  pack:cljs:deps
  {:doc "Generate a ClojureScript JAR file using Uberdeps"
   :depends [test:release generate:project]
   :task
   (let [deps-file (str (fs/path sdk-root "deps.edn"))
         output-file "kubelt-sdk.jar"
         parts ["-M" "-m" "uberdeps.uberjar" "--deps-file" deps-file "--target" output-file]
         command (str/join " " parts)]
     (clojure command))}

  ;; This is pretty gross. Prefer the deps.edn-based solution once we
  ;; figure out how to generate a JAR with no Clojure dependencies.
  generate:cljs:project
  {:doc "Generate a project.clj from shadow-cljs.edn"
   :task
   (let [project-map
         {:name :com.kubelt/sdk
          :version version
          :description "Kubelt SDK"
          :url "https://github.com/kubelt/kubelt"
          :source-paths ["src/main"]
          :repositories [["github" {:url "https://maven.pkg.github.com/kubelt/kubelt"
                                    :creds :gpg}]]}
         shadow-path (str (fs/path sdk-root "shadow-cljs.edn"))
         output-path (str (fs/path "project.clj"))
         ;; The Leiningen project.clj file as a string.
         project-clj (shadow/shadow->lein project-map shadow-path)]
     (spit output-path project-clj))}

  pack:cljs:project
  {:doc "Generate a ClojureScript JAR file using Leiningen"
   :depends [test:release generate:cljs:project]
   :task
   (let [project-file (str (fs/path "project.clj"))]
     (shell "lein jar"))}

  publish:cljs
  {:doc "Publish the SDK as a ClojureScript package"
   :depends [pack:cljs:project #_pack:cljs:deps]
   :task (shell "lein deploy github")}}}
